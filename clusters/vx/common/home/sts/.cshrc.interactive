# do nothing gracefully
if ( $?SGE_O_SHELL || $?PBS_ENVIRONMENT || $?Not_Interactive ) then
	:
else
	
	# misc
	set autologout=0
	set cdpath=(. .. $home)
	set history=256
	set filec
	set fignore=( .o )
	set lineedit
	set notify
	set savestack=~sts/.csh_dirstack
	set savews=~sts/.csh_ws
	set timeout=900
	
        set H=$HOST
	if ( -r /etc/resolv.conf ) then
		set hl=`grep search /etc/resolv.conf | sed 's/search //'`
	else
		set hl=""
	endif
        foreach h ( $hl local localdomain )
                set H=(`basename $H .${h}`)
        end
        unset h
        if ( "${H}" != "" ) then
                set HOST=${H}
        endif
        unset H

	# set up tty
	if ( $?tty ) then
		if ( _"$tty" == _"" ) then
			set tty=(`tty`)
		endif
	else
		set tty=(`tty`)
	endif
	if ( _"$tty" == _"/dev/console" || _"$tty" == _"/dev/ttyv?" ) then
		set On_Console
	endif
	
	set stty_opt=""
	if ( $?SVR4 || $?SOLARIS ) then
		set stty_opt=(echoe istrip)
	endif
	if ( $?SUNOS ) then
		set stty_opt="sistrip"
	endif
	if ( $?FREEBSD ) then
		set stty_opt="sistrip"
	endif
	if ( $?LINUX ) then
		set stty_popt="istrip"
	endif
	
	if ( $?TERM ) then
		if ( $?remote ) then
			echo TERM is $TERM
		endif
	
	 	if ( $TERM == emacs ) then
			set stty_opt=( $stty_opt nl -echo -onlcr )
		endif
	endif
	stty $stty_opt sane erase '' intr  >& /dev/null 
	
	# directory manipulaction
	alias convhome 'echo "\!*"|sed s=\/tmp_mnt==|sed s=$HOME=\~=|sed s=$HOMEDIR=\~='
	alias Convdir	convhome
	alias back	'set back=$old; set old=$cwd; cd $back; unset back; dirs'
	alias po	popd
	alias pu	pushd
	alias pushd	'pushd \!* ; cd .'
	alias popd	'popd  \!* ; cd .'
	#alias cd	'set old=$cwd; chdir \!* '
	
	set   		sstack=(~sts/bin/save_dirstack)
	set   		rstack=(~sts/bin/restore_dirstack)
	alias sstak	source $sstack
	alias rstak	source $rstack
	
	# common typos
	alias sl	ls
	alias ll 	ls -l
	alias ls-l 	ls -l
	alias lsl 	ls -l
	alias lt	ls -t
	alias llt	ls -lt
	alias m	more
	alias mroe	more
	alias moer	more
	alias lsse	more
	alias mk	make
	alias mkae	make
	alias amke	make
	alias grpe	grep
	alias cd.	cd .
	alias c		'cd \!*'
	alias grpe	grep
	alias gerp	grep
	alias netstat-i	netstat -i
	alias ns	netstat
	alias .s	sudo
	alias .S	'sudo -s -u root'
	
	# misc
	alias ssh	'ssh \!* ; cd .' #to reset title bar
	alias h		'history 22'
	alias rm	'mv -f \!* ~sts/.Trash/ |& grep -v dentical'
	alias j		jobs
	alias s.	.s
	alias cz	/usr/local/www/cgi-bin/count_zones.cgi -raw -total
	
	if ( ! $?CC ) then
		alias  cc	gcc
		setenv CC	gcc
		unsetenv CFLAGS
	endif
	
	alias	bindvers 'dig @\!* version.bind txt chaos'
	
	# Mail
	set maildir=( $home/4/lib/Mail )
	set spooldir=( $home/lib/procmail/spool )
	
	set mail=(15 /var/mail/sts /var/mail/sts-fax /var/mail/sts4 /var/mail/sts-spam)
	alias M		Mail
	alias f		'from | tail -22'
	
	# other commands
	if ( $OS == SunOS ) then
		alias	ping	'/usr/sbin/ping -v -s \!* 56 5'
	else if ( $OS != Linux ) then
		alias	ping	'/sbin/ping -c 5 \!*'
	endif
	
	# prompt
	set endprompt = "\!% "
	if ( -w /etc/passwd ) then
		set endprompt = "\!# "
		set Root
	endif
	set	prompt=("$endprompt")
	
	# will be used for window titles
	# which may be embedded within the prompt
	if ( $?TITLE ) then
		set	title=($TITLE)
	else
		set	title=($HOST)
		setenv	TITLE $title
	endif
	
	if ( $?Root ) then
		set title=${TITLE}
	endif
	set	starttitle=""
	set	endtitle=""
	set	starticontitle=""
	set	endicontitle=""
	alias	settitle	'echo -n "${starttitle}\!* ${endtitle}"'
	alias	setititle	'echo -n "${starticontitle}\!* ${endicontitle}"'
	alias	setTitle	'settitle \!*; setititle \!*'
	
	if ( $?TERM ) then
		switch ( _"$TERM" )
		case _"xterm-color":
			if ( $?SOLARIS || $?LINUX ) then
				setenv TERM xterm
			endif
		case _"ansi":
		case _"iris-ansi":
		case _"xterm":
		case _"xterms":
		#case _"vt220":
		case _"vt100":
		case _"vs100":
		case _"vs100s":
			set starttitle="]2;"
			set endtitle=""
			alias	cd	'set old=$cwd; chdir \!* ;echo -n "${starttitle} ${title}  `Convdir ${cwd}`                [${OS} ${RELEASE}] ${endtitle}"'
			#powerline -- how to set terminal character to %
			#alias precmd 'set prompt="`powerline-shell --shell tcsh $?`"'
	
			breaksw
	
		case _"sun":
		case _"sun-cmd":
			if ( ! $?On_Console && $?DISPLAY ) then
				set starttitle="]l"
				set endtitle="\"
				set starticontitle="]L"
				set endicontitle="\"
				alias	cd	'set old=$cwd; chdir \!* ;echo -n "${starttitle} [${OS} ${RELEASE}]     ${title} `Convdir ${cwd}`${endtitle}"; echo -n "${starticontitle}${title}${endicontitle}"'
			endif
			breaksw
	
		default:
			if ( _"$HOST" != _"$HOMEHOST" ) then
				set prompt = ("${HOST} ${endprompt}")
			else
				set prompt = ("${endprompt}")
			endif
			#powerline -- how to set terminal character to %
			#alias precmd 'set prompt="`powerline-shell --shell tcsh $?`"'
		endsw
	endif
	cd.
endif

